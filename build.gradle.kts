import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("org.springframework.boot") version "3.1.2"
    id("io.spring.dependency-management") version "1.1.2"
    id("com.ncorti.ktfmt.gradle") version "0.13.0"
    id("org.flywaydb.flyway") version "9.19.0"
    kotlin("jvm") version "1.8.22"
    kotlin("plugin.spring") version "1.8.22"
    kotlin("plugin.jpa") version "1.8.22"
    kotlin("plugin.allopen") version "1.9.0"
    application
}

group = "my.thingservice"

version = "0.0.1-SNAPSHOT"

java { sourceCompatibility = JavaVersion.VERSION_17 }

repositories { mavenCentral() }

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("org.flywaydb:flyway-core")
    implementation("org.flywaydb:flyway-mysql")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    runtimeOnly("com.mysql:mysql-connector-j")

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude(module = "mockito-core")
    }
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("com.ninja-squad:springmockk:4.0.2")
}

allOpen {
    annotation("jakarta.persistence.Entity")
    // annotation("jakarta.persistence.Embeddable")
    // annotation("jakarta.persistence.MappedSuperclass")
}

ktfmt {
    kotlinLangStyle()
    // maxWidth.set(120)
}

tasks.withType<KotlinCompile> {
    kotlinOptions {
        freeCompilerArgs += "-Xjsr305=strict"
        jvmTarget = "17"
    }
}

tasks.withType<Test> { useJUnitPlatform() }

task<Exec>("dockerBuild") {
    commandLine("docker", "build", "-f", "docker/Dockerfile", "-t", "thing", ".")
}

task<Exec>("dockerRun") {
    commandLine("docker-compose", "--project-name=thing", "run", "--rm", "--service-ports", "thing")
}
